<?xml version="1.0" encoding="UTF-8" standalone="yes"?>
<root>
<name>createShape()</name>

<unsupported_modes>js</unsupported_modes>
<js_mode><description>This part of the Processing API is not supported in JavaScript mode</description></js_mode>

<category>Shape</category>

<subcategory></subcategory>

<type></type>

<example>
<image></image>
<code><![CDATA[
PShape square;  // The PShape object

void setup() {  
  size(100, 100, P2D);
  // Creating the PShape as a square. The corner 
  // is 0,0 so that the center is at 40,40 
  square = createShape(RECT, 0, 0, 80, 80);
}

void draw() {
  shape(square, 10, 10);
}
]]></code>
</example>

<example>
<image></image>
<code><![CDATA[
PShape s;  // The PShape object

void setup() {  
  size(100, 100, P2D);
  // Creating the PShape as a square. The corner 
  // is 0,0 so that the center is at 40,40 
  s = createShape();
  s.fill(0, 0, 255);
  s.noStroke();
  s.vertex(0, 0);
  s.vertex(0, 50);
  s.vertex(50, 0);
  s.end();
}

void draw() {
  shape(s, 25, 25);
}
]]></code>
</example>

<example>
<image></image>
<code><![CDATA[
PShape s;

void setup() {
  size(100, 100, P2D);
  s = createShape(TRIANGLE_STRIP);
  s.vertex(30, 75);
  s.vertex(40, 20);
  s.vertex(50, 75);
  s.vertex(60, 20);
  s.vertex(70, 75);
  s.vertex(80, 20);
  s.vertex(90, 75);
  s.end();
}

void draw() {
  shape(s, 0, 0); 
}
]]></code>
</example>

<example>
<image></image>
<code><![CDATA[
// Define the individual shapes and the group
PShape alien, head, body;

void setup() {
  size(100, 100, P2D);
  alien = createShape(GROUP);
  // Make two shapes  
  head = createShape(ELLIPSE, 0, 0, 50, 50);
  head.noStroke();
  body = createShape(RECT, 0, 45, 50, 40);
  body.fill(0);
  // Add the two "child" shapes to the parent group
  alien.addChild(body);
  alien.addChild(head);
}

void draw() {
  translate(25, 10);
  shape(alien);  // Draw the group
}
]]></code>
</example>


<description><![CDATA[
The <b>createShape()</b> function is used to define a new shape. Once created, this shape can be drawn with the <b>shape()</b> function. The basic way to use the function defines new primitive shapes. One of the following parameters are used as the first parameter: <b>ELLIPSE</b>, <b>RECT</b>, <b>ARC</b>, <b>TRIANGLE</b>, <b>SPHERE</b>, <b>BOX</b>, <b>LINE</b>. The parameters for each of these different shapes are the same as their corrsponding functions: <b>ellipse()</b>, <b>rect()</b>, <b>arc()</b>, <b>triangle()</b>, <b>sphere()</b>, <b>box()</b>, and <b>line()</b>. The first example above clarifies how this works.<br />
<br />
Custom, unique shapes can be made by using <b>createShape()</b> without a parameter. After the shape is started, the drawing attributes and geometry can be set directly to the shape. See the second example above for specifics.<br />
<br />
Geometry that groups vertices to build larger forms such as group of triangles can be created with <b>createShape()</b> using the same parameters as <b>beginShape()</b>. These options are <b>POINTS</b>, <b>LINES</b>, <b>TRIANGLES</b>, <b>TRIANGLE_FAN</b>, <b>TRIANGLE_STRIP</b>, <b>QUADS</b>, and <b>QUAD_STRIP</b>. See the third example above.<br />
<br />
The  <b>createShape()</b> function can also be used to make a complex shape made of other shapes. This is called a "group" and it's created by using the parameter <b>GROUP</b> as the first parameter. See the fourth example above to see how it works.
]]></description>

</root>
